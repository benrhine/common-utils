/** --------------------------------------------------------------------------------------------------------------------
 * Imports / Object declarations: These imports and object declarations are expected to be red. For some reason being a
 * build file IntelliJ cant correctly see them but they are in actuality present and available.
 * ------------------------------------------------------------------------------------------------------------------ */
import static java.lang.Boolean.parseBoolean
import static java.lang.Integer.parseInt

import groovy.time.TimeCategory
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

/** --------------------------------------------------------------------------------------------------------------------
 * Task: Unit Tests - Execute application unit tests (This test set should be validating semi-atomic increments of code)
 *  ----------------------------------------------------------------------------------------------------------------- */
test { testTask ->
    maxParallelForks ((env == "LOCAL") ? 5 : parseInt("$intParallelism"))           // Set max threads to speed up tests
    reports.html.required = htmlReportsEnabled                                      // Configured in gradle.properties
//    ignoreFailures = true

    testLogging { logging ->
        events TestLogEvent.FAILED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_OUT,
                TestLogEvent.STANDARD_ERROR

        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams(parseBoolean("$testShowStandardStreams"))   // Show standard out and standard error of the test JVM(s) on the console
    }

    useJUnitPlatform {
        if (!parseBoolean("$testAllEnabled")) {
            // To run all available tests with ./gradlew clean test set `testAllEnabled` in gradle.properties to true
            excludeTags "int", "slow", "thin", "load", "flowableInt", "adminInt"
        }
    }

    jacoco {
        enabled = jacocoEnabled                                         // Configured in gradle.properties
    }
    finalizedBy unitTestReports                                         // instructs the tests to finish by generating a coverage report

    afterSuite { desc, result ->
        unitTestBuildColor = result.failedTestCount == 0 ? "good" : "danger"

        if (desc.parent) {
            return
        } // Only summarize results for whole modules

        final String summary = "${result.resultType} " +
                "(" +
                "${result.testCount} tests, " +
                "${result.successfulTestCount} successes, " +
                "${result.failedTestCount} failures, " +
                "${result.skippedTestCount} skipped" +
                ") " +
                "in ${TimeCategory.minus(new Date(result.endTime), new Date(result.startTime))}" +
                "\n"

        // Add reports in `testsUnitResults`, keep failed suites at the end
        if (result.resultType == TestResult.ResultType.SUCCESS) {
            rootProject.testsUnitResults.add(0, summary)
        } else {
            rootProject.testsUnitResults += summary
        }
    }
}

//test.finalizedBy combineJaCoCoReports                   // Combine all present JaCoCo reports (exec files) into one

//check.dependsOn adminIntTest
//check.dependsOn flowableIntTest
//check.dependsOn integrationTest                         // Ensure integration tests are ran when ./gradlew build is executed
//check.dependsOn gitChangelogTask                        // Ensure a changelog is generated on each build for reference

if (jacocoEnabled) {
    check.dependsOn jacocoTestCoverageVerification      // Ensure code coverage is enforced when ./gradlew build is executed
}

/** --------------------------------------------------------------------------------------------------------------------
 * Task: Integration Tests - Executes "live" tests against real resources.
 *  ----------------------------------------------------------------------------------------------------------------- */
task integrationTest(type: Test) {
    maxParallelForks ((env == "LOCAL") ? 5 : parseInt("$intParallelism"))   // Set max threads to speed up tests
    reports.html.required = htmlReportsEnabled                              // Configured in gradle.properties
    description = 'Runs integration tests'
    group = 'verification'

    testLogging { logging ->
        events TestLogEvent.FAILED, TestLogEvent.SKIPPED, TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR

        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams(parseBoolean("$testShowStandardStreams"))   // Show standard out and standard error of the test JVM(s) on the console
    }

    useJUnitPlatform {
        includeTags 'int'
    }

    jacoco {
        enabled = jacocoEnabled                                         // Configured in gradle.properties
    }
    finalizedBy integrationTestReports                                  // instructs the tests to finish by generating a coverage report

    afterSuite { desc, result ->
        intTestBuildColor = result.failedTestCount == 0 ? "good" : "danger"

        if (desc.parent) {
            return
        } // Only summarize results for whole modules

        final String summary = "${result.resultType} " +
                "(" +
                "${result.testCount} tests, " +
                "${result.successfulTestCount} successes, " +
                "${result.failedTestCount} failures, " +
                "${result.skippedTestCount} skipped" +
                ") " +
                "in ${TimeCategory.minus(new Date(result.endTime), new Date(result.startTime))}" +
                "\n"

        // Add reports in `testsIntResults`, keep failed suites at the end
        if (result.resultType == TestResult.ResultType.SUCCESS) {
            rootProject.testsIntResults.add(0, summary)
        } else {
            rootProject.testsIntResults += summary
        }
    }
}
